data {
	int<lower=1> K;		  		// number of clusters
	int<lower=2> N; 	  		// number of samples
	int<lower=N+1> L;	    	// number of loci
	matrix[N,N] obsSigma; 		// observed covar
	matrix[N, N] geoDist; 		// matrix of pairwise geographic distance 
	matrix[N, N] timeDist; 		// matrix of pairwise temporal distance 
	vector[N] sampleSize;		// number of chromosomes genotyped for each sample
}
parameters {
	real<lower=0> alpha0;			// sill of the parametric covariance in cluster k
	real<lower=0> alphaD;			// effect of geographic distance in the parametric covariance in cluster k
	real<lower=0> alphaT;			// effect of temporal distance in the parametric covariance in cluster k
	real<lower=0, upper=2>  alpha2;	// exponential slope parameter in the parametric covariance in cluster k
	real<lower=0> gamma;			// global covariance due to shared ancestral mean between all samles
  	real<lower=0> nugget[N]; 		// sample-specific variance (allele sampling error + sample-specific drift)
 	real<lower=0, upper=0.25> binVar;	// (mean.f * (1-mean.f)) averaged over all loci
}
transformed parameters {
		  matrix[N,N] Sigma;		// this specifies the parametric, admixed covariance matrix
			for (i in 1:N){
				for (j in 1:N){
						Sigma[i, j] <- gamma  +  binVar * ( alpha0 * exp(-(alphaD * geoDist[i, j] + alphaT * timeDist[i, j])^alpha2 ));
					if(i==j){
						Sigma[i, i]<- Sigma[i, i] + binVar * (nugget[i] + 1/sampleSize[i]);
					}
				}
			}
		}
model {
	alpha0 ~ exponential(1);								// prior on alpha0
	alphaD ~ exponential(1);								// prior on alphaD
	alphaT ~ exponential(1);								// prior on alphaT
	alpha2 ~ exponential(1);								// prior on alpha2
	for(i in 1:N) nugget[i] ~ exponential(1);				// prior on nugget
	4 * binVar ~ beta(5,1);									// prior on binomial sampling variance
	gamma ~ exponential(1);									// prior on global shared drift
	(L*obsSigma) ~ wishart(L,Sigma);						// likelihood function	
}