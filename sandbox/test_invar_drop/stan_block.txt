data {
	int<lower=1> K;		  		// number of clusters
	int<lower=2> N; 	  		// number of samples
	int<lower=N+1> L;	    	// number of loci
	matrix[N,N] obsSigma; 		// observed covar
	matrix[N, N] geoDist; 		// matrix of pairwise geographic distance
	vector[N] sampleSize;		// number of chromosomes genotyped for each sample
}
parameters {
	real<lower=0> alpha0;				// sill of the parametric covariance in cluster k
	real<lower=0> alphaD;				// effect of geographic distance in the parametric covariance in cluster k
	real<lower=0, upper=2>  alpha2;		// exponential slope parameter in the parametric covariance in cluster k
  	real<lower=0> nugget[N]; 			// sample-specific variance (allele sampling error + sample-specific drift)
	real<lower=0,upper=0.25> gamma;		// global covariance due to shared ancestral allele frequencies
	real<lower=0,upper=1> zeta;			// avg. binomial sampling variance of drift process across loci [mean(e * (1-e))]
}
transformed parameters {
	matrix[N,N] Sigma;		// this specifies the parametric, admixed covariance matrix
	for (i in 1:(N-1)){
		for (j in (i+1):N){
				Sigma[i, j] <- zeta * ( alpha0 * exp(-(alphaD * geoDist[i, j])^alpha2 )) + gamma;
				Sigma[j, i] <- Sigma[i, j];
		}
	}
	for (i in 1:N){
		Sigma[i, i] <- gamma + zeta * (alpha0 + nugget[i] + 1/sampleSize[i]);
	}
}
model {
	alpha0 ~ exponential(1);								// prior on alpha0
	alphaD ~ exponential(1);								// prior on alphaD
	alpha2 ~ exponential(1);								// prior on alpha2
	for(i in 1:N) nugget[i] ~ exponential(1);				// prior on nugget
	gamma ~ uniform(0,0.25);								// prior on global mean
 	zeta ~ uniform(0,0.25);									// prior on drift variance
	(L*obsSigma) ~ wishart(L,Sigma);						// likelihood function
}